{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Cython Speed-up notes\n",
    "\n",
    "While I started coding on *Cython* I found a number of tips and tricks of what to (not) do. This is a collection of those things...\n",
    "\n",
    "## Basic tutorials and tips\n",
    "\n",
    "For the basis, this is a list of documentation that I found useful:\n",
    "* https://cython.readthedocs.io/en/latest/src/tutorial/cython_tutorial.html\n",
    "* https://cython.readthedocs.io/en/latest/src/tutorial/\n",
    "    \n",
    "Some basics tips that will speed up your code significantly:\n",
    "* **Type** your variables : *all* variables, functions inputs, local variables, global variables, etc.\n",
    "* Minimize functions called from other python libraries (avoid overheads)\n",
    "* Try defining all local function as inline\n",
    "* Learn the difference between cdef, def, and pcdef\n",
    "* If not necessary, release the GIL and make it explicit (ie. use `nogil`)\n",
    "\n",
    "## Working with numpy arrays in I/O\n",
    "\n",
    "The first challenge I was confronted to, was handling Numpy arrays. The cython part of our code takes as inputs numpy arrays, and should give as output numpy arrays as well. However, reading and writing from numpy arrays can be slow in cython. Some tutorials mentioned using memory views, other mention that C array give a clear improvement, and overall several different solutions are mentioned. A StackOverflow answer makes a good benchmark between these solutions for a code that only need to create arrays (not taking any inputs) and giving back a numpy array:\n",
    "https://stackoverflow.com/questions/18462785/what-is-the-recommended-way-of-allocating-memory-for-a-typed-memory-view\n",
    "\n",
    "However, here we need to focus on the copying and accessing the data from the numpy array."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext Cython"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "-------- TESTS -------\n",
      "Running numpy buffers\n",
      "0.012333 0.015000 0.086667 0.693000 5.882000 61.836667 699.461333 μs\n",
      "Running cpython.array buffer\n",
      "0.118333 0.112000 0.454667 1.292667 9.133333 94.626667 2712.874000 μs\n",
      "Running cpython.array memoryview\n",
      "1.057667 0.921333 1.100667 1.807000 7.421333 66.377667 698.120333 μs\n",
      "Running cpython.array raw C type with trick\n",
      "0.064667 0.082667 0.413000 1.584667 13.355333 132.008000 3072.307667 μs\n",
      "Running C pointers\n",
      "0.005000 0.007333 0.023333 0.253000 3.104333 34.064333 467.816000 μs\n",
      "Running malloc memoryview\n",
      "0.903000 0.998667 0.931000 1.401333 3.445667 34.143333 481.790667 μs\n",
      "Running argument memoryview\n",
      "0.011333 0.014667 0.088333 0.794667 6.753000 62.516667 700.184333 μs\n"
     ]
    }
   ],
   "source": [
    "%%cython\n",
    "import time\n",
    "import sys\n",
    "\n",
    "from cpython.array cimport array, clone\n",
    "from cython.view cimport array as cvarray\n",
    "from libc.stdlib cimport malloc, free\n",
    "import numpy as np\n",
    "cimport numpy as np\n",
    "\n",
    "cdef int loops\n",
    "\n",
    "def timefunc(name):\n",
    "    def timedecorator(f):\n",
    "        cdef int L, i\n",
    "\n",
    "        print(\"Running\", name)\n",
    "        for L in [1, 10, 100, 1000, 10000, 100000, 1000000]:\n",
    "            np_array = np.ones(L)\n",
    "            start = time.clock()\n",
    "            res_array = f(L, np_array)\n",
    "            end = time.clock()\n",
    "            print(format((end-start) / loops * 1e6, \"2f\"), end=\" \")\n",
    "            sys.stdout.flush()\n",
    "\n",
    "        print(\"μs\")\n",
    "    return timedecorator\n",
    "\n",
    "print()\n",
    "print(\"-------- TESTS -------\")\n",
    "loops = 3000\n",
    "\n",
    "\n",
    "@timefunc(\"numpy buffers\")\n",
    "def _(int L, np.ndarray[double, ndim=1, mode=\"c\"] np_array not None):\n",
    "    cdef int i, j\n",
    "    cdef double d\n",
    "    for i in range(loops):\n",
    "        for j in range(L):\n",
    "            d = np_array[j]\n",
    "            np_array[j] = d*0.\n",
    "    # Prevents dead code elimination\n",
    "    str(np_array[0])\n",
    "    return np_array\n",
    "    \n",
    "@timefunc(\"cpython.array buffer\")\n",
    "def _(int L, np.ndarray[double, ndim=1, mode=\"c\"] np_array not None):\n",
    "    cdef int i, j\n",
    "    cdef double d\n",
    "    cdef array[double] arr, template = array('d')\n",
    "\n",
    "    for i in range(loops):\n",
    "        arr = clone(template, L, False)\n",
    "        for j in range(L):\n",
    "            # initialization\n",
    "            arr[j] = np_array[j]\n",
    "            # access\n",
    "            d = arr[j]\n",
    "            arr[j] = d*2.\n",
    "    # Prevents dead code elimination\n",
    "    return np.asarray(arr)\n",
    "\n",
    "\n",
    "@timefunc(\"cpython.array memoryview\")\n",
    "def _(int L, np.ndarray[double, ndim=1, mode=\"c\"] np_array not None):\n",
    "    cdef int i, j\n",
    "    cdef double d\n",
    "    cdef double[::1] arr\n",
    "\n",
    "    for i in range(loops):\n",
    "        arr = np_array\n",
    "        for j in range(L):\n",
    "            # usage\n",
    "            d = arr[j]\n",
    "            arr[j] = d*0.\n",
    "    # Prevents dead code elimination\n",
    "    return np_array\n",
    "    \n",
    "\n",
    "@timefunc(\"cpython.array raw C type with trick\")\n",
    "def _(int L, np.ndarray[double, ndim=1, mode=\"c\"] np_array not None):\n",
    "    cdef int i\n",
    "    cdef array arr, template = array('d')\n",
    "\n",
    "    for i in range(loops):\n",
    "        arr = clone(template, L, False)\n",
    "        for j in range(L):\n",
    "            # initialization\n",
    "            arr.data.as_doubles[j] = np_array[j]\n",
    "            # usage\n",
    "            d = arr.data.as_doubles[j]\n",
    "            arr.data.as_doubles[j] = d*2.\n",
    "    # Prevents dead code elimination\n",
    "    return np.asarray(arr)\n",
    "\n",
    "\n",
    "@timefunc(\"C pointers\")\n",
    "def _(int L, np.ndarray[double, ndim=1, mode=\"c\"] np_array not None):\n",
    "    cdef int i\n",
    "    cdef double* arrptr\n",
    "\n",
    "    for i in range(loops):\n",
    "        arrptr = <double*> np_array.data\n",
    "        for j in range(L):\n",
    "            d = arrptr[j]\n",
    "            arrptr[j] = d*0.\n",
    "\n",
    "    return np_array\n",
    "\n",
    "@timefunc(\"malloc memoryview\")\n",
    "def _(int L, np.ndarray[double, ndim=1, mode=\"c\"] np_array not None):\n",
    "    cdef int i\n",
    "    cdef double* arrptr\n",
    "    cdef double[::1] arr\n",
    "\n",
    "    for i in range(loops):\n",
    "        arrptr = <double*> np_array.data\n",
    "        arr = <double[:L]>arrptr\n",
    "        for j in range(L):\n",
    "            d = arrptr[j]\n",
    "            arrptr[j] = d*0.\n",
    "\n",
    "    return np_array\n",
    "\n",
    "@timefunc(\"argument memoryview\")\n",
    "def _(int L, double[::1] np_array not None):\n",
    "    cdef int i, j\n",
    "    cdef double d\n",
    "\n",
    "    for i in range(loops):\n",
    "        for j in range(L):\n",
    "            # usage\n",
    "            d = np_array[j]\n",
    "            np_array[j] = d*0.\n",
    "    # Prevents dead code elimination\n",
    "    return np_array"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**In conclusion:**\n",
    " For all cases, you will gain a 2x factor speed up by using a C pointer. Since the memory is already allocated for the numpy array, it is not necessary to use `malloc`. We will adopt the following declaration:\n",
    "~~~~\n",
    "cdef double* arrptr\n",
    "arrptr = <double*> np_array.data\n",
    "~~~~\n",
    "\n",
    "Note that for all functions we declared the numpy array in the function header.\n",
    "\n",
    "\n",
    "## Parallelization and arrays\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
